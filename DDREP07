*PROCESS LIMITS (FIXEDDEC(15,31));
 DDREP07: PROC OPTIONS (MAIN);
 /***********************************************************/
 /*          MEMBER = DDREP07 PLI + DB2                     */
 /***********************************************************/
 /*                                                         */
 /***********************************************************/
 /*                                                         */
 /*npk 17-05-2010 циа том ахамасоука                        */
 /***********************************************************/
 DCL PLIXOPT CHAR(39) VAR EXT    INIT('ERRCOUNT(0)');
 DCL READER  FILE RECORD INPUT   ENV (  FB RECSIZE(080) ) ;
 DCL PRT     FILE RECORD OUTPUT  ENV (  FB RECSIZE(133) CTLASA) ;
 DCL OUTFILE FILE RECORD OUTPUT  ENV (  FB RECSIZE(110) ) ;

 %INCLUDE COBRANDS ;
 DCL   ADDR      BUILTIN;
 DCL   DATE      BUILTIN;
 DCL   TIME      BUILTIN;
 DCL   SUBSTR    BUILTIN;
 DCL   VALIDDATE BUILTIN;
 DCL   DATETIME  BUILTIN;
 DCL   PLIRETC   BUILTIN;
 DCL   EOC           BIT(1)    INIT('0'B);
 DCL   TOT_FETCHED   PIC'ZZZ.ZZZ.ZZ9'    INIT(0);
 DCL   TOT_ANATH     PIC'ZZZ.ZZZ.ZZ9'    INIT(0);
 DCL   TOT_REJECT    PIC'ZZZ.ZZZ.ZZ9'    INIT(0);
 DCL   WS_POSO       FIXED(15,2)  INIT(0);

 DCL OUTREC      CHAR(110);
 DCL 1 OUTREC1   BASED(ADDR(OUTREC )) ,
        2 OUT_CRE_DATE    CHAR(10),
        2 OUT_DELIM_1     CHAR(01) INIT(';') ,
        2 OUT_DB_AG_BR_ID CHAR(04)  ,
        2 OUT_DELIM_2     CHAR(01) INIT(';') ,
        2 OUT_CR_NAME     CHAR(70)  ,
        2 OUT_DELIM_3     CHAR(01) INIT(';') ,
        2 OUT_CIDB_CUST   PIC'ZZZZZZZZ9',
        2 OUT_DELIM_4     CHAR(01) INIT(';') ,
        2 OUT_PROCESS     CHAR(10)  ,
        2 OUT_DELIM_5     CHAR(03) INIT(';') ;

 DCL 1 CC_REC1 ,      /*CONTROL CARD RECORD */
        2 FILLER0     CHAR(01), /*C                     */
        2 CC_DATEFROM CHAR(10),  /*YYYY-MM-DD*//*DATE FROM */
        2 FILLER1     CHAR(01),
        2 CC_DATETO   CHAR(10),  /*YYYY-MM-DD*//*DATE TO   */
        2 FILLER2     CHAR(58);

 DCL 1   ERLINE              ,
      2  ERLIN1       CHAR(1)      INIT('1'),
      2  ERLIN2       CHAR(132)    INIT(' ');

 DCL    LINES1            PIC '999999';
 DCL    PAGES             PIC '9999';
 DCL    L00               FIXED(1) INIT(0) ;
 DCL    RC                BIT(1) INIT('1'B);
 DCL    SYS_DATE          CHAR(6)    INIT(' ');
 DCL    SYS_TIME          CHAR(9)    INIT(' ');
 DCL    WS_DATE           CHAR(10);
 DCL    WS_DATE_TO        CHAR(26);
 DCL    WS_DATE_FROM      CHAR(26);
 DCL    WS_MYDATE         CHAR(10);
 DCL    WS_TOTAL          FIXED(7);
 /*
 */
 /*  == ==== == ======== =========================  */
   EXEC SQL INCLUDE CTDDMD15;
   EXEC SQL INCLUDE SQLCA;
  ON ENDFILE(READER)
  BEGIN ;
       ERLIN1= ' ';
       ERLIN2= ' дем упаявеи йаята    ' ;
       WRITE FILE(PRT) FROM(ERLINE);
       L00 = DIVIDE(10,L00,1,0);
  END;

  OPEN FILE(PRT),FILE(READER),FILE(OUTFILE);

  SYS_DATE = DATE();
  SYS_TIME = TIME();
  LINES1 = 0; PAGES = 0;

  READ  FILE(READER) INTO(CC_REC1);
  IF(CC_DATEFROM = '0000-00-00') THEN DO;
     CC_DATEFROM ='20'!!SUBSTR(SYS_DATE,1,2)!!'-'!!
                   SUBSTR(SYS_DATE,3,2)!!'-'!!SUBSTR(SYS_DATE,5,2) ;
     CC_DATETO   = CC_DATEFROM;
  END;

  RC = VALIDDATE(CC_DATEFROM        ,'YYYY-MM-DD');
  IF RC = '0'B THEN DO ;
       ERLIN1= ' ';
       ERLIN2= ' кахос глеяолгмиа стгм йаята      = '!!CC_DATEFROM;
       WRITE FILE(PRT) FROM(ERLINE);
       GOTO TELOS;
  END;

  RC = VALIDDATE(CC_DATETO          ,'YYYY-MM-DD');
  IF RC = '0'B THEN DO ;
       ERLIN1= ' ';
       ERLIN2= ' кахос глеяолгмиа стгм йаята      = '!!CC_DATETO ;
       WRITE FILE(PRT) FROM(ERLINE);
       GOTO TELOS;

  END;

  WS_DATE_FROM = CC_DATEFROM!!'-00.00.00.000000';
  WS_DATE_TO   = CC_DATETO  !!'-23.59.59.999999';
  PUT SKIP LIST('WS_DATE_FROM = ',WS_DATE_FROM );
  PUT SKIP LIST('WS_DATE_TO   = ',WS_DATE_TO   );

  CALL DECLARE_CUR2;
  CALL OPEN_CUR2;
  CALL HEADER;
  CALL FETCH_ROWS2;
  DO WHILE(^EOC);
     CALL FETCH_ROWS2;
  END; /*DO WHILE END*/
 /*  =============================================  */
 DECLARE_CUR2:    PROC;
    EXEC SQL
    DECLARE CUR2 CURSOR WITH HOLD FOR SELECT
           DATE(B.DAT_DATETIME_PROC),
           B.CHR_DB_AG_BR_ID,
           B.CHR_CR_NAME,
           B.DEC_CIDB_CUST,
           B.FLG_STATUS_PROCESS
    FROM  CTDDMD15  AS B
    WHERE
    ( B.FLG_STATUS_PROCESS = '0'   OR
      B.FLG_STATUS_PROCESS = '3') AND
    B.DEC_GROUP_TYPE = 40 AND
    B.DAT_DATETIME_PROC >= :WS_DATE_FROM AND
    B.DAT_DATETIME_PROC <= :WS_DATE_TO
    ORDER BY
           DATE(B.DAT_DATETIME_PROC),
           B.CHR_DB_AG_BR_ID,
           B.CHR_CR_NAME,
           B.DEC_CIDB_CUST
    WITH UR;
 END DECLARE_CUR2;
 /*  =============================================  */
 OPEN_CUR2:    PROC;
    EXEC SQL OPEN CUR2;
    IF SQLCODE ^= 0 THEN DO;
       ERLIN1= '1';
       ERLIN2= ' ERROR OPENING CURSOR CUR2 SQLCODE= '!!SQLCODE;
       WRITE FILE(PRT) FROM(ERLINE);
       GOTO TELOS;
    END;
 END OPEN_CUR2;
 /*  ---------------------------------------------  */
 FETCH_ROWS2: PROC;
       EXEC SQL FETCH CUR2 INTO
         :WS_DATE ,
         :CTDDMD15.CHR_DB_AG_BR_ID,
         :CTDDMD15.CHR_CR_NAME,
         :CTDDMD15.DEC_CIDB_CUST,
         :CTDDMD15.FLG_STATUS_PROCESS;

    IF ( SQLCODE ^= 0 ) & ( SQLCODE ^= 100) THEN DO;
       ERLIN1= ' ';
       ERLIN2= ' ERROR FETCHING CURSOR CUR2 SQLCODE= '!!SQLCODE;
       WRITE FILE(PRT) FROM(ERLINE);
       GOTO TELOS;
    END;
    ELSE DO;
        IF SQLCODE = 100 THEN DO;
           EOC='1'B;
           CALL CLOSE_CUR2;
           RETURN;
        END;
        ELSE              DO;
             OUT_DELIM_1  =';';
             OUT_DELIM_2  =';';
             OUT_DELIM_3  =';';
             OUT_DELIM_4  =';';
             OUT_DELIM_5  =';';

             OUT_CRE_DATE       = WS_DATE                  ;
             OUT_DB_AG_BR_ID    =
             SUBSTR(CTDDMD15.CHR_DB_AG_BR_ID,1,4) ;
             OUT_CR_NAME        = CTDDMD15.CHR_CR_NAME     ;
             OUT_CIDB_CUST      = CTDDMD15.DEC_CIDB_CUST   ;
             IF CTDDMD15.FLG_STATUS_PROCESS  = '0' THEN DO;
                OUT_PROCESS = 'амахесг ';
                TOT_ANATH =TOT_ANATH + 1 ;
             END;
             ELSE DO;
                OUT_PROCESS = 'амайкгсг';
                TOT_REJECT =TOT_REJECT+ 1 ;
             END;
             WRITE FILE(OUTFILE) FROM(OUTREC);
             CALL PRINT_REPORT;
             TOT_FETCHED = TOT_FETCHED+1;


        END;
   END;

 END FETCH_ROWS2;
 /*  ---------------------------------------------  */
 /*  ---------------------------------------------  */
 CLOSE_CUR2:    PROC;
    EXEC SQL  CLOSE CUR2;
    IF SQLCODE ^= 0 THEN DO;
       ERLIN1= ' ';
       ERLIN2= ' ERROR CLOSING CURSOR CUR2 SQLCODE= '!!SQLCODE;
       WRITE FILE(PRT) FROM(ERLINE);
       GOTO TELOS;
    END;
 END CLOSE_CUR2;
 /*  ---------------------------------------------  */
 HEADER: PROC;
 DCL   HEAD1   CHAR(132) ;
 DCL   HEAD2   CHAR(132) ;
 DCL 1 HEADER1  BASED(ADDR(HEAD1))  ,
        2 H11             CHAR(10)  ,
        2 H11_DELIM       CHAR(01)  ,
        2 H12             CHAR(10)  ,
        2 H12_DELIM       CHAR(01)  ,
        2 H13             CHAR(30)  ,
        2 H13_DELIM       CHAR(01)  ,
        2 H14             CHAR(10)  ,
        2 H14_DELIM       CHAR(01)  ,
        2 H15             CHAR(10)  ,
        2 H15_DELIM       CHAR(01)  ;


 DCL 1 HEADER2  BASED(ADDR(HEAD2))     ,
        2 H21             CHAR(10)  ,
        2 H21_DELIM       CHAR(01)  ,
        2 H22             CHAR(10)  ,
        2 H22_DELIM       CHAR(01)  ,
        2 H23             CHAR(30)  ,
        2 H23_DELIM       CHAR(01)  ,
        2 H24             CHAR(10)  ,
        2 H24_DELIM       CHAR(01)  ,
        2 H25             CHAR(10)  ,
        2 H25_DELIM       CHAR(01)  ;



         H11 = 'глеяолгмиа';
         H21 = 'сумаккацгс';
         H12 = 'йатастгла';
         H22 = 'сумаккацгс';
         H13 = 'омоласиа';
         H23 = 'ояцамислоу';
         H14 = 'йыдийос';
         H24 = 'пекатг';
         H15 = 'аитиокоциа';
         H25 = 'сумаккацгс';

    H11_DELIM = '|';H12_DELIM = '|';H13_DELIM = '|';
    H14_DELIM = '|';H15_DELIM = '|';

    H21_DELIM = '|';H22_DELIM = '|';H23_DELIM = '|';
    H24_DELIM = '|';H25_DELIM = '|';

   PAGES = DIVIDE(LINES1,60,4,0) + 1;
   BANK = 0;
                          /*BRAN = CC_BRANCH;*/
   BRAN = 917;
   PROG = 'DDREP07   ' ;
   CALL COBRANC(BANK,BRAN,PROG,HDR1,HDR2);
   ERLIN1 = '1';
   ERLIN2 = HDR1;
   WRITE FILE (PRT) FROM (ERLINE);                                      BK301680

   ERLIN1 = ' ';
   ERLIN2 = HDR2;
   WRITE FILE (PRT) FROM (ERLINE);                                      BK301680

   ERLIN1 = ' ';
   ERLIN2 = 'ейтупысг амахесеым амайкгсеым '
               !! 'апо : '!!
               SUBSTR(CC_DATEFROM,9,2)!!'.'!!
               SUBSTR(CC_DATEFROM,6,2)!!'.'!!SUBSTR(CC_DATEFROM,1,4)
               !!' еыс : '!!
               SUBSTR(CC_DATETO  ,9,2)!!'.'!!
               SUBSTR(CC_DATETO  ,6,2)!!'.'!!SUBSTR(CC_DATETO  ,1,4)
               !! ' HM/NIA ейтупысгс : '!!SUBSTR(SYS_DATE,5,2)!!'.'
               !!SUBSTR(SYS_DATE,3,2)!!'.'!!'20'
               !!SUBSTR(SYS_DATE,1,2)!! ' ыPA: '
               !!SUBSTR(SYS_TIME,1,2)!! ':'
               !!SUBSTR(SYS_TIME,3,2)!! ':'
               !!SUBSTR(SYS_TIME,5,2);

   WRITE FILE(PRT) FROM(ERLINE);
   ERLIN2 = (78)' '!! 'аяихлос секидас : '!!PAGES;
   WRITE FILE(PRT ) FROM(ERLINE);
   ERLIN2 = (132)'=';

   WRITE FILE(PRT) FROM(ERLINE);

   ERLIN1 = ' ';
   ERLIN2 = HEAD1   ;
   WRITE FILE(PRT) FROM(ERLINE);
   ERLIN2 = HEAD2 ;
   WRITE FILE(PRT) FROM(ERLINE);

   ERLIN2 = (130)'=';
   WRITE FILE(PRT) FROM(ERLINE);

   LINES1 =LINES1 + 7;
 END HEADER;
 /*  ---------------------------------------------  */
 /*  ---------------------------------------------  */
 PRINT_REPORT:PROC;
 DCL   DTL     CHAR(132);
 DCL 1 DETAIL1 BASED(ADDR(DTL))     ,
       2 DTL_CRE_DATE      CHAR(10),
       2 D1_DELIM          CHAR(01)  INIT('|') ,
       2 DTL_DB_AG_BR_ID   CHAR(10),
       2 D2_DELIM          CHAR(01)  INIT('|') ,
       2 DTL_CR_NAME       CHAR(30),
       2 D3_DELIM          CHAR(01)  INIT('|') ,
       2 DTL_CIDB_CUST     PIC'ZZZZZZZZZ9',
       2 D4_DELIM          CHAR(01)  INIT('|') ,
       2 DTL_PROCESS       CHAR(10),
       2 D5_DELIM          CHAR(01)  INIT('|') ;

   DTL_CRE_DATE      =  OUT_CRE_DATE;
   DTL_CR_NAME       =  substr(OUT_CR_NAME,1,30)    ;
   SUBSTR(DTL_DB_AG_BR_ID,4) =  SUBSTR(OUT_DB_AG_BR_ID,1,4) ;
   DTL_CIDB_CUST     =  OUT_CIDB_CUST               ;
   DTL_PROCESS       =  OUT_PROCESS;

   D1_DELIM ='|';
   D2_DELIM ='|';
   D3_DELIM ='|';
   D4_DELIM ='|';
   D5_DELIM ='|';

   ERLIN2 = DTL ;
   WRITE FILE(PRT) FROM(ERLINE);
   LINES1 = LINES1 + 1;
   CALL NEXT_LINE;
 END PRINT_REPORT;
 /*  ---------------------------------------------  */
 /*  ---------------------------------------------  */
 NEXT_LINE: PROC;
     IF MOD(LINES1,60)=0  THEN
     DO;
        CALL HEADER;
        ERLIN1 = ' ';
     END;
 END NEXT_LINE;
 /*  ---------------------------------------------  */
 /*  ---------------------------------------------  */
 TELOS:
       ERLIN1= '1';
       ERLIN2= ' ******************************************** ' ;
       WRITE FILE(PRT) FROM(ERLINE);
       ERLIN2= ' сумока пяоцяаллатос DDREP07 THN = '
               !!SUBSTR(SYS_DATE,5,2)!!'.'
               !!SUBSTR(SYS_DATE,3,2)!!'.'!!'20'
               !!SUBSTR(SYS_DATE,1,2);
       WRITE FILE(PRT) FROM(ERLINE);
       ERLIN1= ' ';
       ERLIN2= ' сумоко сумаккацым = '!!TOT_FETCHED ;
       WRITE FILE(PRT) FROM(ERLINE);
       ERLIN2= ' сумоко амахесеым  = '!!TOT_ANATH   ;
       WRITE FILE(PRT) FROM(ERLINE);
       ERLIN2= ' сумоко амайкгсеым = '!!TOT_REJECT  ;
       WRITE FILE(PRT) FROM(ERLINE);
       ERLIN2= ' ******************************************** ' ;
       WRITE FILE(PRT) FROM(ERLINE);
       CLOSE FILE(PRT),FILE(OUTFILE),FILE(READER);
 END DDREP07;
