 /* дглиоуяциа аявеиоу сумдяолым лекым тее апо то аявеио глеягсиым
    йимгсеым.

    аявеио еисодоу: бй.P.XS.DAILYMOV
    аявеио енодоу:  DK.P.CD.DKDSKTEE
    HMEPOMHNIA:      12/10/97
 */

 PRGM: PROC OPTIONS(MAIN);

 /*** PL/1 OPTIMIZER EXECUTION TIME OPTIONS ***/

 DCL PLIXOPT CHAR(39) VAR EXT  INIT ('ERRCOUNT(0)');

 DCL CZX3VDAT CHAR(6); FETCH CZXDATEP;
 DCL CZXDATEP ENTRY OPTIONS(INTER,ASM) ;

 /*** EXTERNAL & BUILT-IN FUNCTIONS ***/

 DCL  EKXOR   ENTRY( CHAR(3), CHAR(14) )
               OPTIONS( ASSEMBLER, INTER ); FETCH EKXOR;

 DCL  ADDR      BUILTIN;
 DCL  ONCODE    BUILTIN;
 DCL  SUBSTR    BUILTIN;
 DCL  TRANSLATE BUILTIN;

 DCL ALFA CHAR(25) INIT('ABCDEFGHIJKLMNOQRSUVWXYZP') STATIC;
 DCL BHTA CHAR(25) INIT('ABcdEZHhIKkMNnOpPrTYuXwx/') STATIC;

 /***  DATE SIMULATION DEFINED FUNCTION  ****/

 DATE: PROC IRREDUCIBLE RETURNS(CHAR(6)) ;
            CALL CZXDATEP( CZX3VDAT) ;
            RETURN (CZX3VDAT) ;
       END DATE ;

 /* емтокес INCLUDE */
 %INCLUDE RECMOV;      /* ояислос тоу аявеиоу йимгсеыс */
 %INCLUDE RECTEE;      /* ояислос тоу аявеиоу пкгяылгс сумдяолым */

 /* дгкысг аявеиым */
 DCL MOVFILE FILE RECORD SEQUENTIAL INPUT
     ENV( FB RECSIZE(74) );

 DCL TEEFILE FILE RECORD SEQUENTIAL OUTPUT
     ENV( FB RECSIZE(60) );

 DCL PRINTF FILE RECORD SEQUENTIAL OUTPUT
     ENV( FB RECSIZE(133) CTLASA);


 /* цяаллес ейтупысгс */
 /* пяытг цяаллг йахе секидас */

 DCL 1 HEAD_LINE,
     2 HEAD11 CHAR(1)  INIT('1'),
     2 HEAD12 CHAR(20) INIT('EMpOPIKH TPApEZA A.E.'),
     2 HEAD13 CHAR(85) INIT(' '),
     2 HEAD14 CHAR(8)  INIT('DK424'),
     2 HEAD15 CHAR(8) INIT(' '),
     2 HMER   PIC '99.99.9999',
     2 HEAD16 CHAR(1)  INIT(' ');

 /* вягсилопоиеитаи циа тгм ейтупысг тгс глеяолгмиас */
 DCL 1 TODAY1,
     2 CDD  CHAR(2),
     2 CMM  CHAR(2),
     2 CYY  CHAR(4);
 DCL TODAY CHAR(8) BASED(ADDR(TODAY1));

 DCL 1 FIRST_LINE,
     2 FLF1  CHAR(1)  INIT('-'),
     2 FL1   CHAR(13) INIT('dIABArTHKAN: '),
     2 INRECS PIC 'ZZZZ99',
     2 FL2   CHAR(33) INIT(' RECORDS ApO TO APXEIO KINHrExN.'),
     2 FLF2  CHAR(80) INIT(' ');

 DCL 1 SECOND_LINE,
     2 SLF1  CHAR(1)  INIT('0'),
     2 SL1   CHAR(13) INIT('cPAuTHKAN: '),
     2 OUTRECS  PIC 'ZZZZ99',
     2 SL2   CHAR(33) INIT(' RECORDS rTO APXEIO DK.P.CD.TEE1.'),
     2 SLF2  CHAR(80) INIT(' ');

  /* FLAGS */
 DCL 1 FLAGS,
       2 NEOF PIC '9' INIT(1);

 /* COUNTERS */
 DCL 1 COUNTERS,
     2 IN_CNT  FIXED(7,0) INIT(0),
     2 OUT_CNT FIXED(7,0) INIT(0);

 /* паяалетяои йатастглатос */
 DCL KATMAPIC PIC'999';
 DCL KATMACHA CHAR(3)  BASED(ADDR(KATMAPIC));
 DCL KATMATXT CHAR(14) INIT(' ');


 /* летабкгтес циа то MANIPULATION Tгс глеяолгмиас */
 DCL DATEPIC PIC '(6)9';
 DCL DATECHA CHAR(6) BASED(ADDR(DATEPIC));
 DCL YEARTMP CHAR(2);

 /* аявг пяоцяаллатос */

 /*** FILE INITIALISATIONS ***/
 OPEN FILE(MOVFILE);
 ON ENDFILE(MOVFILE) NEOF= 0;

 OPEN FILE(PRINTF);
 OPEN FILE(TEEFILE);

 /* EKTYпысг епийежакидас */

 DATECHA= DATE();
 CDD= SUBSTR(DATECHA,5,2);
 CMM= SUBSTR(DATECHA,3,2);
 YEARTMP= SUBSTR(DATECHA,1,2);

 IF(YEARTMP > '90') THEN
    CYY= '19' !! YEARTMP;
 ELSE CYY= '20' !! YEARTMP;

 HMER= TODAY;
 WRITE FILE(PRINTF) FROM(HEAD_LINE);

 READ FILE(MOVFILE) INTO(RECISKAT);
 DO WHILE( NEOF = 1);
    IN_CNT= IN_CNT+1;

    ARLOG=    KSMARLOG;
    /* DATE8 */
    DATEPIC= KSMHMNIA;
    DDAY= SUBSTR(DATECHA,5,2);
    DMONTH= SUBSTR(DATECHA,3,2);
    YEARTMP= SUBSTR(DATECHA,1,2);

    IF(YEARTMP > '90') THEN
       DYEAR= '19' !! YEARTMP;
    ELSE DYEAR= '20' !! YEARTMP;

    ARMHTRWO= KSMAREP;
    ARKAT= KSMSKAT;
    ARTERM= KSMSTERM;
    ARTRT= KSMARTRT;
    ARKATAST= KSMKATLE;

    KATMAPIC= KSMSKAT;
    CALL EKXOR(KATMACHA,KATMATXT);
    IF SUBSTR(KATMATXT,1,3) = 'AMV' THEN KATMATXT = 'kAh.Kxd.KAT/MA';
    ELSE KATMATXT = TRANSLATE(KATMATXT,BHTA,ALFA);

    ONKATAST= KATMATXT;

    POSO= KSMPOSON * 100; /* 2002 */
    KWDCRDB= KSMKWDEG;

    WRITE FILE(TEEFILE) FROM(RECSYND);
    OUT_CNT=OUT_CNT+1;

    READ FILE(MOVFILE) INTO(RECISKAT);
 END;

 INRECS= IN_CNT;
 WRITE FILE(PRINTF) FROM(FIRST_LINE);

 OUTRECS= OUT_CNT;
 WRITE FILE(PRINTF) FROM(SECOND_LINE);

 CLOSE FILE(PRINTF);
 CLOSE FILE(TEEFILE);
 CLOSE FILE(MOVFILE);

 END PRGM;



